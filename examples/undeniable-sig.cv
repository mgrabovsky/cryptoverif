(*************************************************************
 *                                                           *
 *       Cryptographic protocol verifier                     *
 *                                                           *
 *       Bruno Blanchet and David Cadé                       *
 *                                                           *
 *       Copyright (C) ENS, CNRS, INRIA, 2005-2015           *
 *                                                           *
 *************************************************************)

(*

    Copyright ENS, CNRS, INRIA 
    contributors: Bruno Blanchet, Bruno.Blanchet@inria.fr
                  David Cadé

This software is a computer program whose purpose is to verify 
cryptographic protocols in the computational model.

This software is governed by the CeCILL-B license under French law and
abiding by the rules of distribution of free software.  You can  use, 
modify and/ or redistribute the software under the terms of the CeCILL-B
license as circulated by CEA, CNRS and INRIA at the following URL
"http://www.cecill.info". 

As a counterpart to the access to the source code and  rights to copy,
modify and redistribute granted by the license, users are provided only
with a limited warranty  and the software's author,  the holder of the
economic rights,  and the successive licensors  have only  limited
liability. 

In this respect, the user's attention is drawn to the risks associated
with loading,  using,  modifying and/or developing or reproducing the
software by the user in light of its specific status of free software,
that may mean  that it is complicated to manipulate,  and  that  also
therefore means  that it is reserved for developers  and  experienced
professionals having in-depth computer knowledge. Users are therefore
encouraged to load and test the software's suitability as regards their
requirements in conditions enabling the security of their systems and/or 
data to be ensured and,  more generally, to use and operate it in the 
same conditions as regards security. 

The fact that you are presently reading this means that you have had
knowledge of the CeCILL-B license and that you accept its terms.

*)
(* Undeniable signature scheme of 
   http://www.di.ens.fr/~pointche/Documents/Papers/2001_pkc.pdf
   Section 4.2 *)

proof {
  crypto rom(H);
  crypto group_to_exp_strict(exp) *;
  SArename s;
  show_game occ;
  replace 59 "exp(y, @3_x_259)";
  replace 44 "exp(y, @3_x_256[@i_252])";
  replace 23 "exp(y, @3_x_259[@i_254])";
  crypto gdh(exp);
  success
}

param NS, NC.

type Z [large,bounded].
type G [large,bounded].


(* Gap Diffie-Hellman problem *)

proba pGDH.
expand GDH(G, Z, g, exp, mult, pGDH).

(* Random oracle model, optimized to avoid creating a new random hash when we test exp(hash(k,x'), e) = r' *)

type hashkey [large,fixed].

define ROM_hash_exp(key, hashinput, hash (*, hashoracle, qH*)) {

param Nh, N, Neq.

fun hash(key, hashinput):G.

equiv rom(hash)
      !Nh new k:key;
      	  (!N Ohash(x:hashinput) := hash(k,x),
      	   !Neq Oeq(x':hashinput, e: Z, r':G) := exp(hash(k,x'), e) = r')
      <=(#Oeq / |G|)=> [computational]
      !Nh (!N Ohash(x:hashinput) := 
              find[unique] j <= N suchthat defined(x[j],r[j]) && x = x[j] then r[j] else
	      new r:G; r,
      	   !Neq Oeq(x':hashinput, e: Z, r':G) := 
              find[unique] j <= N suchthat defined(x[j],r[j]) && x' = x[j] then exp(r[j], e) = r' else
	      false).

}

expand ROM_hash_exp(hashkey, bitstring, H).
param NH [noninteractive].
channel H_in, H_out.
let hashoracle = ! NH in(H_in, xh:bitstring); out(H_out, H(hk,xh)). 

(* Query *)

event forgery.

query event forgery ==> false.


channel start, c0, c1, c2, c3, c4.

let Psign =
        in(c1, m:bitstring);
	let h = H(hk, m) in
	let s: G = exp(h, x) in
	out(c2, s).

let Pconfirm =
        in(c3, (m': bitstring, s': G));
	if (exp(H(hk, m'), x) = s') then
	       (find j <= NS suchthat defined(m[j],s[j]) && m' = m[j] && s' = s[j] then 
	          out(c4, true)
		else 
		  event forgery;
		  out(c4, true)
               )
	else 
		out(c4, false).
		(* The zero-knowledge proof is omitted *)

process
	in(start, ());
	new hk: hashkey;
        new x: Z;
       	let y = exp(g, x) in
	out(c0, y);
	(!NS Psign | !NC Pconfirm | hashoracle)

(* EXPECTED
All queries proved.
0.036s (user 0.032s + system 0.004s), max rss 19968K
END *)
